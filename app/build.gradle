apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    lintOptions {
        abortOnError false
    }

    dataBinding {
        enabled = true
    }

    testBuildType "debug"

    defaultConfig {
        applicationId "com.projects.melih.baking"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "app_name", "Baking Time"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            applicationId "com.projects.melih.bakingtest"
            dimension "default"
        }
        prod {
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    supportLibrary = "27.1.1"
    aac = "1.1.1"
    room = "1.1.0"
    dagger = "2.15"
    retrofit = "2.3.0"
    glide = "4.6.1"
    leakcanary = "1.5.4"
}

dependencies {
    // Support
    implementation "com.android.support:appcompat-v7:${supportLibrary}"
    implementation "com.android.support:support-annotations:${supportLibrary}"
    implementation "com.android.support:design:${supportLibrary}"
    implementation "com.android.support:recyclerview-v7:${supportLibrary}"
    implementation "com.android.support:cardview-v7:${supportLibrary}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    // Android Architecture Components
    implementation "android.arch.lifecycle:extensions:${aac}"
    implementation "android.arch.lifecycle:runtime:${aac}"
    annotationProcessor "android.arch.lifecycle:compiler:${aac}"

    // Room components
    implementation "android.arch.persistence.room:runtime:${room}"
    annotationProcessor "android.arch.persistence.room:compiler:${room}"
    androidTestImplementation "android.arch.persistence.room:testing:${room}"

    // Dagger
    implementation "com.google.dagger:dagger:${dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${dagger}"

    // Dagger.Android
    implementation "com.google.dagger:dagger-android-support:${dagger}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${dagger}"

    // Network
    implementation "com.squareup.retrofit2:converter-gson:${retrofit}"
    implementation "com.squareup.retrofit2:retrofit:${retrofit}"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    // Image and Video
    implementation "com.github.bumptech.glide:glide:${glide}"
    annotationProcessor "com.github.bumptech.glide:compiler:${glide}"
    implementation 'com.google.android.exoplayer:exoplayer:2.8.0'

    // Utils
    implementation 'com.jakewharton.timber:timber:4.6.0'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakcanary}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanary}"

    implementation 'com.google.code.gson:gson:2.8.0'
    androidTestImplementation 'com.google.code.gson:gson:2.8.0'
    testImplementation 'com.google.code.gson:gson:2.8.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation "com.android.support:support-annotations:${supportLibrary}"
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    androidTestImplementation "android.arch.core:core-testing:${aac}"

    androidTestImplementation 'org.mockito:mockito-android:2.6.3'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}

apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        property "sonar.projectName", "Baking (Java Lint)"
        property "sonar.projectKey", "baking.java.lint"
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.sourceEncoding", "UTF-8"
        property 'sonar.language', 'java'
    }
}